#
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(openslp, 2.0.0, openslp-devel@lists.sourceforge.net)
AC_CONFIG_HEADERS(config.h)
AC_CONFIG_SRCDIR(slpd/slpd_main.c)

AM_INIT_AUTOMAKE

#
# Checks for programs

AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_LIBTOOL
AM_PROG_LEX
AC_PROG_YACC

#
# Checks for libraries

AC_CHECK_LIB(resolv, inet_aton)
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(nsl, gethostbyname)

#
# Checks for header files

AC_HEADER_STDC
AC_CHECK_HEADERS([arpa/inet.h fcntl.h inttypes.h limits.h memory.h netdb.h stddef.h stdint.h stdlib.h string.h strings.h sys/socket.h unistd.h])

#
# Checks for types

OPENSLP_CHECK_TYPE(socklen_t, int,
	[defined to size_t if <sys/socket.h> does not support socklen_t data type])
OPENSLP_STRUCT_SA_RESTORER

#
# Checks for library functions

AC_REPLACE_FNMATCH
AC_FUNC_FORK
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_SELECT_ARGTYPES
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([alarm gethostbyname gethostname gettimeofday isascii memchr memmove memset select socket strcasecmp strchr strdup strerror strncasecmp strrchr strstr strtol strtoul])

#
# We don't want /usr/etc or /usr/var

test "$prefix" = NONE && prefix=/usr
if test "$prefix" = '/usr'; then 
    # We don't want /usr/etc and /usr/var
    test "$sysconfdir" = '${prefix}/etc' && sysconfdir=/etc
    test "$sharedstatedir" = '${prefix}/com' && localstatedir=/var
    test "$localstatedir" = '${prefix}/var' && localstatedir=/var
fi

#
# Take care of the debug stuff

AC_ARG_ENABLE(debug,
[  --enable-debug          Turn on debugging (default off)],
debug=$enableval, debug=no)

CFLAGS="${CFLAGS:=}"
LDFLAGS="${LDFLAGS:=}"

case "$debug" in
  yes) CFLAGS="-g -DDEBUG -D_REENTRANT=1 $CFLAGS" ;;
  no)  CFLAGS="-DNDEBUG -D_REENTRANT=1 $CFLAGS" ;;
  *) AC_MSG_ERROR(bad value ${debug} for --enable-debug) ;;
esac

#
# System type macros

case "$host_os" in
  *linux*) CFLAGS="$CFLAGS -DLINUX" ;;
  *solaris*) CFLAGS="$CFLAGS -DSOLARIS";;
  *aix*) CFLAGS="$CFLAGS -DAIX";;
  *hpux*) CFLAGS="$CFLAGS -DHPUX";;
esac

#
# SLPv1 support can be turned off

AC_ARG_ENABLE(slpv1,
[  --enable-slpv1          Turn on SLPv1 support (default on)],
slpv1=$enableval, slpv1=yes)

#
# Conditional for optional SLPv1 support

AM_CONDITIONAL(ENABLE_SLPv1, test "$slpv1" = "yes")

if test "$slpv1" = "yes"; then
    AC_DEFINE(ENABLE_SLPv1, 1, [defined if struct sigaction has member sa_restorer])
fi

#
# Handle predicates 

AC_ARG_ENABLE(predicates,
[  --disable-predicates    Turn off predicates],
predicates=$enableval, predicates=yes)

#
# Conditional for optional predicates support

AM_CONDITIONAL(ENABLE_PREDICATES, test "$predicates" = "yes")

if test "$predicates" = "yes"; then
    AC_DEFINE(ENABLE_PREDICATES, 1, [defined if predicates are enabled])
fi

#
# Enable asyncronous support in libslp SLP API library 

AC_ARG_ENABLE(async-api,
[  --enable-async-api      Turn on asyncronous support (default off)],
asyncapi=$enableval, asyncapi=no)

#
# Conditional for optional asyncronous support in libslp SLP API library

AM_CONDITIONAL(ENABLE_ASYNC_API, test "$asyncapi" = "yes")

if test "$asyncapi" = "yes"; then
    AC_DEFINE(ENABLE_ASYNC_API, 1, [defined if the async SLP API support is enabled])
fi

#
# Enable security support 

AC_ARG_ENABLE(slpv2-security,
[  --enable-slpv2-security Turn on security support (default off)],
slpv2security=$enableval,
slpv2security=no)

AC_CHECK_LIB(pthread, pthread_create)
AC_CHECK_LIB(m, log10)
AC_CHECK_LIB(crypto, DSA_sign) 
AC_CHECK_FUNCS(DSA_verify DSA_size DSA_free SHA1)

#
# Conditional for optional security support in libslp SLP API library

AM_CONDITIONAL(ENABLE_SLPv2_SECURITY, test "$slpv2security" = "yes")
if test "$slpv2security" = "yes"; then
    AC_DEFINE(ENABLE_SLPv2_SECURITY, 1, [defined if the SLPv2 authentication support is enabled])
fi

#
# Set C/C++ compiler specific warning/optimization flags

if test X"$GCC" = X"yes"; then        
    CFLAGS="$CFLAGS -Wall"
    if test X"$debug" = X"yes"; then
       CFLAGS="$CFLAGS -Werror"
    fi
    if test X"$debug" = X"no"; then 
       OPTFLAGS="-O3"
    fi
    
elif $CC -V 2>&1 | grep "WorkShop Compilers"; then
    # Allow C++ style comments
    CFLAGS="$CFLAGS -xCC" 
    if test X"$debug" = X"no"; then
        OPTFLAGS="-fast"
        if echo $CC | grep "xarch=v9"; then
            # "-fast" sets -xarch=v8 disabling 64-bit mode, enable it again
            OPTFLAGS="$OPTFLAGS -xarch=v9"
        fi
    fi
elif echo $host_os | grep -i "osf" >/dev/null; then
    CFLAGS="$CFLAGS -std"
    if test X"$debug" = X"no"; then
        OPTFLAGS="-O"
    fi
else
    if test X"$debug" = X"no"; then
        OPTFLAGS="-O"
    fi
fi
CFLAGS="$CFLAGS $OPTFLAGS"

AC_CONFIG_FILES([Makefile
		 common/Makefile
		 libslpattr/Makefile
		 libslp/Makefile
		 slpd/Makefile
		 slptool/Makefile
		 test/Makefile])
AC_OUTPUT

